# capellaextras_build_index 

Couchbase when creating an index will immediatly start building the index. This means scanning the entire keyspace and 
updating the index as it goes. If you have a large amount of data this can take some time. If you are also planning on 
building other indexes in the collection then this will result in multiple scans.

To work around the issue couchbase allows you to create an index but defer its build. This is really usefaul when building 
multiple indexes in the same collection. Since it can scan the keyspace once and update each index in parallel. 

While the existing couchbase-capella provider gives a way to do deferred index builds if the index was deleted terraform
would only recreate the index but it would never be built again. This action is designed to allow builds to be triggered
any time an index is created.

## Action Per Index
This is not the most efficient way to do this since the keyspace will need to be scanned each time an index is built in 
the same collection. However it its the easiest to implement. In cases where your adding multiple indexes to a new bucket
with no data this will still be fast. Or if you are adding an additional index to an existing bucket it should still be ok.

```terraform
locals {
  org_id = "aaaaaaaa-8f0c-22222-865e-bbbbbbbbbbbb"
  indexes = {
    idx1 = {
      org_id     = local.org_id
      index_keys = ["foo", "bar"]
    }
    idx2 = {
      org_id     = local.org_id
      index_keys = ["foo", "bar"]
    }
  }
}

resource "couchbase-capella_query_indexes" "index" {
  for_each        = local.indexes
  bucket_name     = couchbase-capella_bucket.new_free_tier_bucket.name
  cluster_id      = couchbase-capella_free_tier_cluster.new_free_tier_cluster.id
  organization_id = each.value.org_id
  project_id      = couchbase-capella_project.new_project.id
  index_name      = each.key
  index_keys      = each.value.index_keys
  with = {
    defer_build = true
  }
  lifecycle {
    action_trigger {
      events  = [after_create]
      actions = [action.capellaextras_build_index.build_index[each.key]]
    }
  }
}


action "capellaextras_build_index" "build_index" {
  for_each = couchbase-capella_query_indexes.index
  config {
    bucket_name     = each.value.bucket_name
    cluster_id      = each.value.cluster_id
    organization_id = each.value.organization_id
    project_id      = each.value.project_id
    scope_name      = each.value.scope_name
    collection_name = each.value.collection_name
    index_names     = [each.key]
  }
}
```

## Building Multiple Indexes
A more efficent method would be to create all of the indexes for a collection then build them at the same time. This will 
mean the key space only needs to be scanned once for all indexes. However this can be challanging due to how the capella
provider implements deferred builds. 

So do to this we can collect all indexes into a `terraform_data` resource that will trigger every apply. This is not an 
issue since any indexes that are already built will be skipped so triggering this on every apply will still be efficient.

```terraform
locals {
  org_id = "aaaaaaaa-8f0c-22222-865e-bbbbbbbbbbbb"
  indexes = {
    idx1 = {
      org_id     = local.org_id
      index_keys = ["foo", "bar"]
    }
    idx2 = {
      org_id     = local.org_id
      index_keys = ["foo", "bar"]
    }
  }
}

resource "couchbase-capella_query_indexes" "index" {
  for_each        = local.indexes
  bucket_name     = couchbase-capella_bucket.new_free_tier_bucket.name
  cluster_id      = couchbase-capella_free_tier_cluster.new_free_tier_cluster.id
  organization_id = each.value.org_id
  project_id      = couchbase-capella_project.new_project.id
  index_name      = each.key
  index_keys      = each.value.index_keys
  with = {
    defer_build = true
  }
}

resource "terraform_data" "foo" {
  triggers_replace = {
    timestamp = timestamp()
  }
  lifecycle {
    action_trigger {
      events  = [after_create]
      actions = [action.capellaextras_build_index.build_index]
    }
  }
  depends_on = [couchbase-capella_query_indexes.index]
}

action "capellaextras_build_index" "build_index" {
  config {
    bucket_name     = couchbase-capella_bucket.new_free_tier_bucket.name
    cluster_id      = couchbase-capella_free_tier_cluster.new_free_tier_cluster.id
    organization_id = local.org_id
    project_id      = couchbase-capella_project.new_project.id
    index_names     = [for idx in couchbase-capella_query_indexes.index : idx.index_name]
  }
}
```
 

<!-- action schema generated by tfplugindocs -->
## Schema

### Required

- `bucket_name` (String) The bucket name where the index is located.
- `cluster_id` (String) The cluster id where the index is located.
- `index_names` (List of String) The name of the index to build.
- `organization_id` (String) The organization id where the index is located.
- `project_id` (String) The project id where the index is located.

### Optional

- `collection_name` (String) The name of the collection where the index is located.
- `scope_name` (String) The name of the scope where the index is located.

